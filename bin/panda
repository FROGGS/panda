#!/usr/bin/env perl6
use v6;
use Pies;
use Panda::Ecosystem;
use Panda::Fetcher;
use Panda::Builder;
use Panda::Tester;
use Panda::Installer;
use File::Mkdir;

class Panda_the_Dog is Pies {
    has $!srcdir;
    has $!destdir;
    has $!statefile;
    has $!projectsfile;

    submethod BUILD {
        callsame; # attribute initialization
        $!ecosystem = Panda::Ecosystem.new(
            statefile    => $!statefile,
            projectsfile => $!projectsfile,
        );
        $!fetcher   = Panda::Fetcher.new(srcdir => $!srcdir);
        $!builder   = Panda::Builder.new(srcdir => $!srcdir);
        $!tester    = Panda::Tester.new(srcdir => $!srcdir);
        $!installer = Panda::Installer.new(
            srcdir  => $!srcdir,
            destdir => $!destdir
        );
    }

    multi method announce(Str $what) {
        say "==> $what"
    }

    multi method announce('fetching', Pies::Project $p) {
        self.announce: "Fetching {$p.name}"
    }

    multi method announce('building', Pies::Project $p) {
        self.announce: "Building {$p.name}"
    }

    multi method announce('testing', Pies::Project $p) {
        self.announce: "Testing {$p.name}"
    }

    multi method announce('installing', Pies::Project $p) {
        self.announce: "Installing {$p.name}"
    }

    multi method announce('success', Pies::Project $p) {
        self.announce: "Succesfully installed {$p.name}"
    }

    multi method announce('depends', Pair $p) {
        self.announce: "{$p.key.name} depends on {$p.value.name}"
    }
}

subset Command of Str where
    'install' | 'list' | 'update';

sub installprojects($panda, @args) {
    for @args -> $x {
        try { $panda.resolve($x) };
        say $! if $!;
    }
}

sub listprojects($panda) {
    for $panda.ecosystem.project-list -> $p {
        my $x = $panda.ecosystem.get-project($p);
        printf "%-20s\t%s\n", $x.name, $x.metainfo<description>;
    }
}

sub execute(Command $c, $panda, @args) {
    given $c {
        when 'install' { 
            installprojects($panda, @args);
        }
        when 'list' {
            listprojects($panda);
        }
        when 'update' {
            $panda.ecosystem.update;
        }
    }
}

# default opts for MAIN
if %*ENV<PANDA_DEFAULT_OPTS> {
    @*ARGS = %*ENV<PANDA_DEFAULT_OPTS> ~ ' ' ~ @*ARGS;
}

my $panda;
{
    my $pandadir = %*ENV<HOME> ~ '/.panda';
    mkdir $pandadir, :p unless $pandadir.IO ~~ :d;

    my $projectsfile = "$pandadir/projects.json";
    unless $projectsfile.IO ~~ :f {
        run "wget http://feather.perl6.nl:3000/list -O $projectsfile";
    }

    $panda = Panda_the_Dog.new(
        srcdir       => "$pandadir/src",
        destdir      => %*ENV<HOME> ~ '/.perl6',
        statefile    => "$pandadir/state",
        projectsfile => "$pandadir/projects.json"
    );
}

multi MAIN (Command $command, *@args) {
    execute($command, $panda, @args);
}

multi MAIN () {
    while prompt('panda> ') -> $c {
        my ($command, @args) = $c.split(' ');
        try {
            execute($command, $panda, @args);
            CATCH {
                say "Unknown command: $command";
            }
        }
    }
    say ''; # the newline after exiting the REPL
}

# vim: ft=perl6
