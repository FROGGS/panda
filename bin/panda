#!/usr/bin/env perl6
use v6;
use Pies;
use Panda::Ecosystem;
use Panda::Fetcher;
use Panda::Builder;
use Panda::Tester;
use Panda::Installer;

class Panda_the_Dog is Pies {
    has $!srcdir;
    has $!destdir;
    has $!statefile;
    has $!projectsfile;

    # I liked Moose and its BUILD better. Seriously, why is Perl 6
    # more annoying to use than Moose? That doesn't make any sense
    method init {
        $!ecosystem = Panda::Ecosystem.new(
            statefile    => $!statefile,
            projectsfile => $!projectsfile,
        );
        $!fetcher   = Panda::Fetcher.new(srcdir => $!srcdir);
        $!builder   = Panda::Builder.new(srcdir => $!srcdir);
        $!tester    = Panda::Tester.new(srcdir => $!srcdir);
        $!installer = Panda::Installer.new(
            srcdir  => $!srcdir,
            destdir => $!destdir
        );
    }

    multi method announce(Str $what) {
        say "==> $what"
    }

    multi method announce('fetching', Pies::Project $p) {
        self.announce: "Fetching {$p.name}"
    }

    multi method announce('building', Pies::Project $p) {
        self.announce: "Building {$p.name}"
    }

    multi method announce('testing', Pies::Project $p) {
        self.announce: "Testing {$p.name}"
    }

    multi method announce('installing', Pies::Project $p) {
        self.announce: "Installing {$p.name}"
    }

    multi method announce('success', Pies::Project $p) {
        self.announce: "Succesfully installed {$p.name}"
    }

    multi method announce('depends', Pair $p) {
        self.announce: "{$p.key.name} depends on {$p.value.name}"
    }
}

if %*ENV<PANDA_DEFAULT_OPTS> {
    @*ARGS = %*ENV<PANDA_DEFAULT_OPTS> ~ ' ' ~ @*ARGS;
}

sub MAIN (*@modules) {
    my $pandadir = %*ENV<HOME> ~ '/.panda';
    unless $pandadir.IO ~~ :d {
        mkdir $pandadir;
    }
    my $projectsfile = "$pandadir/projects.json";
    unless $projectsfile.IO ~~ :f {
        run "wget http://feather.perl6.nl:3000/list -O $projectsfile";
    }
    my $panda = Panda_the_Dog.new(
        srcdir       => "$pandadir/src",
        destdir      => %*ENV<HOME> ~ '/.perl6',
        statefile    => "$pandadir/state",
        projectsfile => "$pandadir/projects.json"
    );
    $panda.init(); # I liked Moose and its BUILD better
    if @modules {
        $panda.resolve($_) for @modules;
    } else {
        while prompt('panda> ') -> $c {
            my ($command, @args) = $c.split(' ');
            given $command {
                when 'install' {
                    for @args -> $x {
                        try { $panda.resolve($x) };
                        say $! if $!;
                    }
                }
                default {
                    say "Unknown command: $command";
                }
            }
        }
        say; # the newline after exiting the REPL
    }
}

# vim: ft=perl6
